# -*- mode: sh -*-

# fzf-repo-widget.zsh
# author: Seong Yong-ju <sei40kr@gmail.com>

__home_to_tilde() {
    while read -r a_path; do
        if [[ "$a_path" == "${HOME}"* ]]; then
            echo "~${a_path:${#HOME}}"
        else
            echo "$a_path"
        fi
    done
}

__tilde_to_home() {
    read -r a_path

    if [[ "$a_path" == '~'* ]]; then
        echo "${HOME}${a_path:1}"
    else
        echo "$a_path"
    fi
}

fzf-repo-widget() {
    {
        [[ -d $HOME'/.dotfiles' ]] && echo "${HOME}/.dotfiles"
        [[ -d $HOME'/.emacs.d' ]] && echo "${HOME}/.emacs.d"
        find "${HOME}/develop/workspace" -mindepth 1 -maxdepth 1 -type d
    } | __home_to_tilde | eval "fzf ${FZF_DEFAULT_OPTS}" | __tilde_to_home | read -r a_path

    if [[ "$a_path" == '' ]]; then
        zle reset-prompt
        return
    fi

    if [[ "$TMUX" == '' ]]; then
        BUFFER="cd $(shescape "$a_path")"
        zle accept-line
        return
    fi

    local session_name="$(basename "$a_path" | tr -d '.')"
    tmux switch-client -t "$session_name" 2>/dev/null && return

    local current_session="$(tmux display-message -p '#S')"
    if [[ "$current_session" = <-> ]]; then
        tmux rename-session -t "$current_session" -- "$session_name"
        BUFFER="cd $(shescape "$a_path")"
        zle accept-line
    else
        tmux new-session -dc "$a_path" -s "$session_name"
        tmux switch-client -t "$session_name"
    fi
}

fzf-repo-widget "$@"
